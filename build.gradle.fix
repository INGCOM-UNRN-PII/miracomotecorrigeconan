import com.github.spotbugs.snom.SpotBugsTask

import java.time.Duration

plugins {
    id 'java'
    id 'pmd'
    id 'checkstyle'
    id 'application'
    id 'com.github.spotbugs' version '6.0.7'
    id 'jacoco'
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(21))
    }
}

application {
    mainClass = 'ar.unrn.LoaderApp'
}

run {
    standardInput = System.in
}

group = 'ar.unrn'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.2'
}

test {
    useJUnitPlatform()
    timeout = Duration.ofMinutes(1) // Un minuto para resolver cuelgues en los tests.
    finalizedBy jacocoTestReport
}


jacoco {
    toolVersion = "0.8.12"
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required.set(true)
        csv.required.set(true)
    }
}

pmd {
    toolVersion = "7.0.0"
    consoleOutput = true
    ignoreFailures = true
    ruleSetFiles = files('config/pmd/programacion2.xml')
    ruleSets = []
}

spotbugs {
    toolVersion = '4.8.3'
}

tasks.withType(SpotBugsTask).configureEach {
    reports {
        html.required.set(true)
        xml.required.set(true)
    }
}

checkstyle {
    toolVersion = '10.9.2'
    configFile = rootProject.file("config/checkstyle/checkstyle.xml")
}

